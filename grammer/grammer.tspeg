PROGRAM    := toplevels=TOPLEVEL* $
TOPLEVEL   := STRUCTDEF | FUNCDEF | EXTERNFUNC
STRUCTDEF  := _ 'struct' __ name=IDENTIFIER _ '{' _ f=FIELDS? _ '}' _
              .fields = FIELD[] { return this.f ? this.f.v : []; }
FIELDS     := h=FIELD t={ _ ',' _ v=FIELD}*
              .v = FIELD[] { return [this.h].concat(t.map(e => e.v)); }
FIELD      := name=IDENTIFIER _ ':' _ type=TYPE
FUNCDEF    := _ v=VISIBILITY? _ 'fn' __ name=IDENTIFIER _ '\(' _ p=FIELDS? _ '\)' _ '->' _ returnType=TYPE _ '{' _ b=BODY? _ '}' _
              .params = FIELD[] { return this.p ? this.p.v : []; }
              .body = STATEMENT[] { return this.b ? this.b.v : []; }
              .isPublic = boolean { return Boolean(this.v); }
VISIBILITY := 'pub'
BODY       := h=STATEMENT _ ';' _ t={v=STATEMENT _ ';' _}*
              .v = STATEMENT[] { return [this.h].concat(t.map(e => e.v)); }
STATEMENT  := LET | RETURN | CALL
LET        := _ 'let' __ name=IDENTIFIER _ '=' _ e=EXPR
              .expr = SUM | FAC | CALL | DOT | ATOM { return this.e.v; }
EXPR       := s=SUM
              .v = SUM | FAC | CALL | DOT | ATOM { return this.s.fac || this.s; }
SUM        := h=FAC t={ _ op='\+|-' _ v=FAC }*
              .operands = (FAC | CALL | DOT | ATOM)[] { return [this.h.call || this.h].concat(t.map(e => e.v.call || e.v)); }
              .fac = FAC | CALL | DOT | ATOM | undefined { return this.operands.length === 1 ? this.operands[0] : undefined; }
FAC        := h=CALL t={ _ op='\*|/' _ v=CALL }*
              .operands = (CALL | DOT | ATOM)[] { return [this.h.dot || this.h].concat(t.map(e => e.v.dot || e.v)); }
              .call = CALL | DOT | ATOM | undefined { return this.operands.length === 1 ? this.operands[0] : undefined; }
CALL       := h=DOT t={_ '\(' _ args=ARGS? _ '\)' _}*
              .callee = DOT | ATOM { return this.h.atom || this.h; }
              .args = (SUM | FAC | CALL | DOT | ATOM)[][] { return this.t.map(e => e.args ? e.args.v : []); }
              .dot = DOT | ATOM | undefined { return this.args.length === 0 ? this.callee : undefined; }
ARGS       := h=EXPR t={_ ',' _ v=EXPR }*
              .v = (SUM | FAC | CALL | DOT | ATOM)[] { return [this.h.v].concat(t.map(e => e.v.v)); }
DOT        := receiver=ATOM a={ _ op='\.' _ v=IDENTIFIER }*
              .accessors = string[] { return this.a.map(e => e.v); }
              .atom = ATOM | undefined { return this.accessors.length === 0 ? this.receiver : undefined; }
ATOM       := VARREF | INTCONST
VARREF     := name=IDENTIFIER
INTCONST   := s='[\-\+]?\d+'
              .value = number { return parseInt(this.s); }
RETURN     := _ 'return' _ e=EXPR
              .expr = SUM | FAC | CALL | DOT | ATOM { return this.e.v; }
EXTERNFUNC := _ 'import' __ externalName=EXTNAME _ 'as' __ 'fn' _ importName=IDENTIFIER _ '\(' _ p=FIELDS? _ '\)' _ '->' _ returnType=TYPE _ ';' _
              .params = FIELD[] { return this.p ? this.p.v : []; }
EXTNAME    := '[a-zA-Z][a-zA-Z\d_]*(\.[a-zA-Z][a-zA-Z\d_]*)*'
TYPE       := name=IDENTIFIER
IDENTIFIER := '[a-zA-Z][a-zA-Z\d_]*'
_          := '\s*'
__         := '\s+'
